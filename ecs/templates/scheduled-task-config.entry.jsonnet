// Scheduled task definition entry for ecs-scheduled-task
// Returns full config: { base, plugins, region, rules, task }
// Used by ecschedule.jsonnet (.rules/.base.cluster) and ecspresso.jsonnet (.plugins/.region/.base.cluster)
// Task definition (camelCase) is generated by scheduled-task-definition.entry.jsonnet
//
// Usage: jsonnet -V ENV=dev -V NAME=test-batch \
//               -V ACCOUNT_ID=<id> -V AWS_REGION=ap-northeast-1 templates/scheduled-task-config.entry.jsonnet
//
// External variables:
//   ENV:        environment name (dev, qa, stg, prd)
//   NAME:       scheduled task name (e.g., test-batch)
//   ACCOUNT_ID: AWS account ID
//   AWS_REGION: AWS region

local env = std.extVar('ENV');
local name = std.extVar('NAME');
local registry = import '../registry.jsonnet';
local globalConfig = import '../config.jsonnet';

local baseConfig = registry.scheduled_tasks[name][env];
local batch = baseConfig.base;
local prefix = globalConfig.env;
local region = globalConfig.region;
local accountId = globalConfig.accountId;

local container_name = globalConfig.helpers.buildName(prefix, batch.name);
local task_definition_family = globalConfig.helpers.buildName(prefix, '%s-td' % batch.name);

// Merge globalConfig defaults with base overrides (base takes priority).
// Nested objects requiring partial override are merged explicitly.
local rules = {
  platform_version: globalConfig.service.platform_version,
  network_configuration: globalConfig.network_configuration.scheduled_task,
} + batch.rules;

local task = globalConfig.task + batch.task + {
  container_definitions: globalConfig.task.container_definitions + batch.task.container_definitions,
  runtime_platform: globalConfig.task.runtime_platform + std.get(batch.task, 'runtime_platform', {}),
};

// Build and return full config (ecschedule uses .rules, ecspresso uses .task)
baseConfig {
  plugins: [{ name: 'tfstate', config: { url: globalConfig.helpers.buildTfstateUrl(accountId) } }],
  region: region,
  rules: [
    {
      name: globalConfig.helpers.buildName(prefix, batch.name),
      description: rules.description,
      scheduleExpression: rules.schedule_expression,
      taskDefinition: task_definition_family,
      containerOverrides: [{ name: container_name, command: rules.container_overrides.command }],
      role: rules.events_role,
      group: 'batch:%s' % task_definition_family,
      launch_type: 'FARGATE',
      platform_version: rules.platform_version,
      propagateTags: 'TASK_DEFINITION',
      network_configuration: rules.network_configuration,
    },
  ],
  task: task {
    family: task_definition_family,
    tags: batch.tags,
    container_definitions: task.container_definitions {
      name: container_name,
      image: '%s:%s' % [self.image_repository, self.image_tag],
      log_configuration: {
        log_driver: 'awslogs',
        options: {
          log_group: globalConfig.helpers.buildLogGroup(prefix, batch.name),
          create_group: 'true',
          region: region,
          stream_prefix: 'batch',
        },
      },
    },
  },
}
